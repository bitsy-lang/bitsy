use crate::ast::*;
use crate::common::*;
use crate::context::Context;

grammar;

pub Namespace: Namespace = {
    <decls:Decl*> => Namespace {
        decls,
    }
}

pub Decl: Decl = {
    <mod_def:ModDef> => {
        Decl::ModDef(mod_def)
    },
    <enum_def:EnumDef> => {
        Decl::EnumDef(enum_def)
    },
    <struct_def:StructDef> => {
        Decl::StructDef(struct_def)
    },
}

pub ModDef: ModDef = {
    <visibility:Visibility> "mod" <name:Ident>
        <ports: Port*>
        <components: Component*>
        <wires: Wire*>
    "end"
    => {
        ModDef {
            name,
            visibility,
            ports,
            components,
            wires,
        }
    },
}

pub StructDef: StructDef = {
    <visibility:Visibility> "struct" "shape" <name:Ident> <params:TypeDefParams?>
        <fields:StructField*>
    "end"
    => {
        StructDef {
            name,
            params: Context::from(params.unwrap_or(vec![])),
            visibility,
            fields,
        }
    }
}

pub StructField: StructField = {
    "field" <ident:Ident> "of" <shape:Shape> => StructField(ident, shape),
}

pub EnumDef: EnumDef = {
    <visibility:Visibility> "enum" "shape" <name:Ident> <params:TypeDefParams?>
        <alts:EnumAlt*>
    "end"
    => {
        EnumDef {
            name,
            params: Context::from(params.unwrap_or(vec![])),
            visibility,
            alts,
        }
    }
}

pub TypeDefParams: Vec<(String, Kind)> = {
    "<" <param0:TypeDefParam> <param_rest:("," TypeDefParam)*> ">" => {
        let mut result = vec![param0];
        for (_s, param) in param_rest {
            result.push(param);
        }
        result
    },
}

pub TypeDefParam: (String, Kind) = {
    <ident:Ident> "of" "Nat" => (ident, Kind::Nat),
    <ident:Ident> "of" "Shape" => (ident, Kind::Shape),
}

pub EnumAlt: EnumAlt = {
    <ctor_name:Ctor> <payload_shape:("of" <Shape>)?> => {
        EnumAlt(ctor_name, payload_shape)
    }
}

pub Port: Port = {
    "port" <port_name:Ident> <pins:Pin*> => Port(port_name.to_string(), pins),
}

pub Pin: Pin = {
    <direction:Direction> <name:Ident> "of" <shape:Shape>
    => Pin(name, direction, shape, DomainRef::default())
}

pub Direction: Direction = {
    "incoming" => Direction::Incoming,
    "outgoing" => Direction::Outgoing,
}

pub Shape: ShapeRef = {
    <ident:Ident> "<" <shape_params:ShapeArgs> ">" => ShapeRef(ident, shape_params),
    <ident:Ident> => ShapeRef(ident, vec![]),
}

pub ShapeArgs: Vec<ShapeArg> = {
    <shape_param0:ShapeArg> <shape_param_rest:("," ShapeArg)*> => {
        let mut result = vec![shape_param0];
        for (_s, shape_param) in shape_param_rest {
            result.push(shape_param);
        }
        result
    }
}

pub ShapeArg: ShapeArg = {
    <n:Nat> => ShapeArg::Nat(n),
    <shape:Shape> => ShapeArg::Shape(shape),
}

pub Domain: DomainRef = {
    <ident:Ident> => DomainRef(ident),
}

pub Component: Component = {
    <visibility:Visibility> "reg" <ident:Ident> "of" <shape:Shape> <domain:("@" Domain)?> <init:RegInit?> => {
        Component::Reg(
            ident,
            Visibility::Public,
            RegComponent {
                shape,
                domain: DomainRef::default(),
                init,
            }
        )
    },

    <visibility:Visibility> "mod" <ident:Ident> "of" <mod_def_ref:ModDefRef> => {
        Component::Mod(
            ident,
            Visibility::Public,
            ModComponent {
                mod_def_ref,
            }
        )
    },

    <visibility:Visibility> "gate" <ident:Ident> "of" <gate_ref:GateRef> => {
        Component::Gate(
            ident,
            Visibility::Public,
            GateComponent {
                gate_ref,
            }
        )
    },
}

pub RegInit: Box<Expr> = {
    "init" <expr: Expr> => expr
}

pub Nat: u64 = {
    <n:r"[0-9]+"> => n.parse().unwrap(),
}

pub Value: Value = {
    "true" => Value::Bit(true),
    "false" => Value::Bit(false),
    <n:r"[0-9]+"> => Value::Word(n.parse().unwrap()),
}

pub TupleElts: Vec<Box<Expr>> = {
    <e0:Expr> <e_rest:("," <Expr>)*> => {
        let mut result = vec![e0];
        for e in e_rest {
            result.push(e);
        }
        result
    }
}

pub Visibility: Visibility = {
    <visibility:"pub"?> => if visibility.is_some() { Visibility::Public } else { Visibility::Private }
}

pub ModDefRef: ModDefRef = {
    <ident:Ident> => ModDefRef(ident.to_string())
}

pub GateRef: GateRef = {
    <ident:Ident> => GateRef(ident.to_string())
}

pub Wire: Wire = {
    <visibility:Visibility> "wire" <sink:TerminalRef> "<=" <expr:Expr> => {
        Wire(visibility, sink, expr)
    },
}

pub Expr: Box<Expr> = {
    "let" <v:Ident> <shape:("of" Shape)?> "=" <def:Expr> ";" <body:Expr> => Box::new(Expr::Let(v, def, shape.map(|x| x.1), body)),
    "match" <subject:Expr> "{"
        <patterns:MatchArm*>
    "}" => Box::new(Expr::Match(subject, patterns)),
    <expr:ExprEq> => expr,
}

pub ExprEq: Box<Expr> = {
    <op0:ExprAdd> "==" <op1:ExprEq> => Box::new(Expr::Eq(op0, op1)),
    <op0:ExprAdd> "!=" <op1:ExprEq> => Box::new(Expr::Neq(op0, op1)),
    <expr:ExprAdd> => expr,
}

pub ExprAdd: Box<Expr> = {
    <op0:ExprSlice> "+" <op1:ExprAdd> => Box::new(Expr::Add(op0, op1)),
    <expr:ExprSlice> => expr,
}

pub ExprSlice: Box<Expr> = {
    <expr:ExprBase> "[" <index:Slice> "]" <end:"?"?> => {
        Box::new(Expr::Slice(expr, index))
    },
    <expr:ExprBase> => expr,
}

pub Slice: Box<Expr> = {
    <expr: Expr> => expr,
}

pub ExprBase: Box<Expr> = {
    <x:Ident> <path_parts:("." Ident)*> => {
        let mut full_x = String::from(x);
        for path_part in path_parts {
            full_x.push_str(".");
            full_x.push_str(&path_part.1);
        }
        Box::new(Expr::Var(full_x))
    },
    <value:Value> => Box::new(Expr::Lit(value)),
    "(" <expr:Expr> ")" => expr,
    "${" <struct_fields:StructFields> "}" => {
        Box::new(Expr::Struct(struct_fields))
    },
    "@" <ctor_name:Ident> <payload:( "(" Expr ")")?>  => Box::new(Expr::Enum(ctor_name, payload.map(|p| p.1))),
    "tuple" "(" <elts:TupleElts?>")" => Box::new(Expr::Tuple(elts.unwrap_or_default())),
}

pub MatchArm: MatchArm = {
    <pattern:MatchPattern> "=>" <expr:Expr> ";" => MatchArm(pattern, expr)
}

pub MatchPattern: Box<MatchPattern> = {
    <ctor:Ctor> <args: MatchPatternArgs?> => Box::new(MatchPattern::Ctor(ctor, args.unwrap_or(vec![]))),
    <v:Ident> => Box::new(MatchPattern::Var(v)),
    <v:Value> => Box::new(MatchPattern::Lit(v)),
    "otherwise" => Box::new(MatchPattern::Otherwise),
}

pub MatchPatternArgs: Vec<Box<MatchPattern>> = {
    "(" <first:MatchPattern> <rest:("," MatchPattern)*> ")" => {
        let mut results = vec![first];
        for pat in rest {
            results.push(pat.1);
        }
        results
    }
}


pub StructFields: Vec<(FieldName, Box<Expr>)> = {
    <field0:(Ident "=" Expr )> <field_rest:("," Ident "=" Expr)*> ","? => {
        let mut results = vec![(field0.0.to_string(), field0.2).clone()];
        for field in &field_rest {
            results.push((field.1.to_string(), field.3.clone()));
        }
        results
    }
}

pub TerminalRef: TerminalRef = {
    <component_name:Ident> "." <port_name:Ident> => TerminalRef(component_name, port_name)
}

pub Ctor: String = {
    "@" <name:Ident> => name,
}

pub Ident: String = {
    <ident:r"[_A-Za-z][_A-Za-z0-9]*"> => ident.to_string()
}

match {
    r"//[^\n\r]*[\r\n]" => {},
    r"[\r\n]*" => {},
    r" " => {},
    _,
}
